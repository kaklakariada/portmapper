plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'jacoco'
    id "com.github.hierynomus.license" version "0.16.1"
    id "org.sonarqube" version "5.1.0.4882"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
    id "io.github.gradle-nexus.publish-plugin" version "2.0.0"
    id 'com.github.ben-manes.versions' version '0.51.0'
    id 'org.gradle.crypto.checksum' version '1.4.0'
}

repositories {
    mavenCentral()
    maven {
        url 'http://4thline.org/m2'
        allowInsecureProtocol true
    }
    flatDir { dirs 'lib' }
}

version = '2.2.4'
group 'com.github.kaklakariada'

java {
    toolchain {
        def javaVersion = project.hasProperty('javaVersion') ? project.getProperty('javaVersion') : 11
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    failOnError = true
    options.addBooleanOption('html5', true)
    //options.addStringOption('Xwerror', '-quiet')
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:all'
    options.compilerArgs << '-Werror'
    options.encoding = 'UTF-8'
}

test {
    if (logger.infoEnabled) {
        testLogging.showStandardStreams = true
    }
    jvmArgs '-XX:+HeapDumpOnOutOfMemoryError', '-enableassertions'
}

processResources {
    rename(/(\w+)_en.properties/, '$1.properties')
    rename(/(\w+)_zh_CN.properties/, '$1_zh.properties')
    filter { line -> line.replaceAll(/@VERSION_NUMBER@/, project.version) }
}

shadowJar {
    archiveBaseName = 'portmapper'
    archiveVersion = null
}

def mainClass = 'org.chris.portmapper.PortMapperStarter'

jar {
    manifest {
        attributes 'Main-Class': mainClass
    }
}

tasks["build"].dependsOn(shadowJar)

task createSha512ChecksumExecutableJar(type: org.gradle.crypto.checksum.Checksum, dependsOn: [tasks.shadowJar]) {
    files = files(tasks.shadowJar.outputs.files)
    outputDir = new File(project.buildDir, "libs-checksums")
    algorithm = org.gradle.crypto.checksum.Checksum.Algorithm.SHA512
    appendFileNameToChecksum = false
}

tasks.shadowJar.configure {
    finalizedBy createSha512ChecksumExecutableJar
}

task run(type: JavaExec, dependsOn: shadowJar) {
    classpath = shadowJar.outputs.files
    mainClass = mainClass
    workingDir = rootProject.projectDir
}

artifacts {
    archives shadowJar
}

dependencies {
    implementation 'args4j:args4j:2.37'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'org.slf4j:jul-to-slf4j:2.0.16'
    implementation 'ch.qos.logback:logback-classic:1.5.8'
    implementation 'com.miglayout:miglayout-swing:11.4.2'
    implementation 'org.jdesktop.bsaf:bsaf:1.9.2'
    implementation ':sbbi-upnplib:1.0.4'
    implementation 'org.fourthline.cling:cling-support:2.1.2'
    implementation 'org.bitlet:weupnp:0.1.4'

    runtimeOnly 'commons-jxpath:commons-jxpath:1.1' // Version 1.1. required by sbbi

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:5.13.0'
}

license {
    header = file('gradle/license-header.txt')
}




jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
}

sonar {
  properties {
    property("sonar.organization", "kaklakariada-github")
    property("sonar.host.url", "https://sonarcloud.io")
  }
}

tasks["sonarqube"].dependsOn(tasks["jacocoTestReport"])

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = 'UPnP Portmapper'
                description = 'A tool for managing port forwardings via UPnP'
                url = 'https://github.com/kaklakariada/portmapper'

                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kaklakariada'
                        name = 'Christoph'
                        email = 'kaklakariada@chp1.net'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/kaklakariada/portmapper.git'
                    developerConnection = 'scm:git:https://github.com/kaklakariada/portmapper.git'
                    url = 'https://github.com/kaklakariada/portmapper'
                }
            }
        }
    }
}


signing {
    def signingKey = findProperty("signingKey")
    def signingPassword = findProperty("signingPassword")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}

nexusPublishing {
    packageGroup = project.group
    repositories {
        sonatype {
            stagingProfileId = "4f1b95618be44"
        }
    }
}

def isNonStable = { String version ->
  def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
  def regex = /^[0-9,.v-]+(-r)?$/
  return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
  gradleReleaseChannel = "current"
  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}
